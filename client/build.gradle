apply plugin: 'kotlin2js'
apply plugin: 'org.jetbrains.kotlin.frontend'

group 'com.chrynan'
version '1.0.0'

repositories {
    mavenCentral()
    jcenter()
}

dependencies {
    compile "org.jetbrains.kotlin:kotlin-stdlib-js"

    compile project(":common")
    compile project(":graphql-query")
    compile project(":locator")

    compile "org.jetbrains.kotlinx:kotlinx-html-js:0.6.12"

    compile "io.ktor:ktor-client-js:1.2.1"

    compile "io.ktor:ktor-client-json-js:1.2.1"

    compile "io.ktor:ktor-client-logging-js:1.2.1"

    compile "org.jetbrains.kotlinx:kotlinx-serialization-runtime-js:0.11.0"
}

compileKotlin2Js {
    kotlinOptions.metaInfo = true
    kotlinOptions.sourceMap = true
    kotlinOptions.moduleKind = 'commonjs'
    kotlinOptions.main = "call"
}

kotlinFrontend {
    npm {
        dependency "text-encoding" // production dependency
    }
    webpackBundle {
        bundleName = "main"
    }
}

task assembleWeb(type: Sync) {
    def scriptList = []
    configurations.compile.each { File file ->
        from(zipTree(file.absolutePath), {
            includeEmptyDirs = false
            include { fileTreeElement ->
                def path = fileTreeElement.path
                def includeFile = path.endsWith(".js")

                if (includeFile && !path.endsWith(".meta.js")) {
                    scriptList << "<script defer type=\"text/javascript\" src=\"../js/${path.substring(path.lastIndexOf("/") + 1)}\"></script>"
                }

                includeFile
            }
        })
    }
    from compileKotlin2Js.destinationDir
    into "${rootProject.projectDir}/Theme/chRyNaN.codes/1.0.0/js" // TODO this should be updated to not be static values

    from "${rootProject.projectDir}/client/build/bundle/main.bundle.js"
    into "${rootProject.projectDir}/Theme/chRyNaN.codes/1.0.0/js"

    doLast {
        def html = """
            <!DOCTYPE html>
            <html lang="en">
            <head>
                <meta charset="UTF-8">
                <title>chRyNaN Codes</title>
            
                <link rel="stylesheet" href="https://fonts.googleapis.com/icon?family=Material+Icons">
                <link rel="stylesheet" href="https://code.getmdl.io/1.3.0/material.indigo-pink.min.css">
                <script defer src="https://code.getmdl.io/1.3.0/material.min.js"></script>
                
                <script defer type="text/javascript" src="../js/main.bundle.js"></script>
            </head>
            <body>
            </body>
            </html>
            """.stripIndent()
        new File("${rootProject.projectDir}/Theme/chRyNaN.codes/1.0.0/js/test.html").text = html
    }

    dependsOn classes
}

assemble.dependsOn assembleWeb